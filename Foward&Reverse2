data work.LowWind work.HighWind; 
    if _N_=1 then do;  
       if 0 then set pg3.storm_final
                (keep=Season Name BasinName MaxWindMPH);
 	   declare hash Storm
               (dataset:'pg3.storm_final(where=(MaxWindMPH ne .))', 
                ordered:'ascending', multidata:'yes');
	   Storm.definekey('MaxWindMPH');
	   Storm.definedata('Season','Name','BasinName','MaxWindMPH');
	   Storm.definedone();
	   declare hiter Stm('Storm');
    end;
    do i=1 to 5;
       if i=1 then Stm.first();
       else Stm.next();
       output work.LowWind;
    end;
    do i=1 to 5;
       if i=1 then Stm.last();
       else Stm.prev();
       output work.HighWind;
    end;
    drop i;
run;

title 'Storms with Lowest Maximum Winds';
proc print data=work.LowWind;
run;
title;

title 'Storms with Highest Maximum Winds';
proc print data=work.HighWind;
run;
title;
